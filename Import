order_file = ALLTRIM(ThisForm.Text1.Value)

**WPS**
ON ERROR sh=CREATEOBJECT("ET.Application") 

**EXCEL**
sh=CREATEOBJECT("Excel.Application") 

ON ERROR 

IF NOT FILE(order_file) THEN 
   ThisForm.Text1.SetFocus 
   RETURN 
ENDIF 

*********************************
SELECT new_order
DELETE ALL

sh.WORKBOOKS.OPEN(order_file)              &&開啟Excel檔
rr=sh.SHEETS(1).UsedRange.Rows.Count       &&有數據的總行數
cc=sh.SHEETS(1).UsedRange.Columns.Count

tot_no = 0   &&總筆數
ng01   = 0   &&品號異常筆數
ng02   = 0   &&交期錯誤筆數
ng03   = 0   &&單價錯誤筆數

key_no   = 1
step_no  = 0
step_key = ">>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>"

customer_id = ALLTRIM(ThisForm.Text4.Value)
currency_id = ALLTRIM(ThisForm.Text9.Value)
data_date = ThisForm.Text8.Value     &&單據日期
data_date = SUBSTR(data_date,1,4)+SUBSTR(data_date,6,2)+SUBSTR(data_date,9,2)

**[匯入訂單]**
FOR i=2 TO rr
    IF ThisForm.Check1.Value = 1 THEN
       IF ISNULL(sh.CELLS(i,1).Value) THEN 
          LOOP 
       ENDIF 
    ELSE
       IF ISNULL(sh.CELLS(i,2).Value) THEN 
          LOOP 
       ENDIF 
    ENDIF
    
    STORE ' ' TO d0,d1,d2,d3,d4,d6,d7,d9
    STORE 0 TO d5,d8
    
    tot_no = tot_no+1
    
    step_no=IIF(step_no>=100,1,step_no+1)
    ThisForm.Label1.Caption = "資料轉入中"+SUBSTR(step_key,1,step_no)
   
    d5 = sh.CELLS(i,3).Value                                                            &&數量       
    d7 = IIF(ISNULL(sh.CELLS(i,5).Value),'',ALLTRIM(sh.CELLS(i,5).Text))                &&備註
    d9 = ALLTRIM(STR(key_no))                                                           &&序號
    d9 = SUBSTR('0000',1,4-LEN(d9))+d9
    
    IF ThisForm.Check1.Value = 1 THEN
       d1 = IIF(ISNULL(sh.CELLS(i,1).Value),'',ALLTRIM(sh.CELLS(i,1).Text))             &&客戶品號     
       **品號**
       TEXT TO SELECT_copmg TEXTMERGE NOSHOW PRETEXT 2
       SELECT MG001,MG002,MG003
       FROM COPMG
       WHERE MG001 = '<<customer_id>>' AND MG003 = '<<d1>>'
       ENDTEXT
       =SQLEXEC(id_key,SELECT_copmg,"v_copmg")
       SELECT v_copmg
       IF EOF() THEN
          d2 = ''
       ELSE
          d2 = MG002
       ENDIF
       USE IN v_copmg 
    ELSE
       d2 = IIF(ISNULL(sh.CELLS(i,2).Value),'',ALLTRIM(sh.CELLS(i,2).Text))              &&品號
       **客戶品號**
       TEXT TO SELECT_copmg TEXTMERGE NOSHOW PRETEXT 2
       SELECT MG001,MG002,MG003
       FROM COPMG
       WHERE MG001 = '<<customer_id>>' AND MG002 = '<<d2>>'
       ENDTEXT
       =SQLEXEC(id_key,SELECT_copmg,"v_copmg")
       SELECT v_copmg
       IF EOF() THEN
          d1 = ''
       ELSE
          d1 = MG003
       ENDIF
       USE IN v_copmg
    ENDIF

    **預交日期**
    IF ISNULL(sh.CELLS(i,4).Value) THEN 
       d0 = 'D'
       ng02 = ng02+1
    ELSE
       d6 = ALLTRIM(sh.CELLS(i,4).Text)
       IF LEN(d6) <> 8 THEN
          d0 = 'D'
          ng02 = ng02+1
       ELSE 
          a = LEFT(d6,4)+"."+SUBSTR(d6,5,2)+"."+RIGHT(d6,2)
          IF a <> DTOC(CTOD(a)) THEN
             d0 = 'D'
             ng02 = ng02 + 1
          ENDIF       
       ENDIF
    ENDIF 
    
    **品名規格單位**
    TEXT TO SELECT_invmb TEXTMERGE NOSHOW PRETEXT 2
         SELECT MB001,MB002,MB003,MB004
           FROM INVMB
          WHERE MB001 = '<<d2>>'
    ENDTEXT
    =SQLEXEC(id_key,SELECT_invmb,"v_invmb")
    SELECT v_invmb
    IF EOF() THEN 
       d0 = d0+"N"
       ng01 = ng01+1
    ELSE 
       d3 = MB002
       d4 = MB003
    ENDIF
    USE IN v_invmb
   
    DO CASE
    CASE ThisForm.Combo2.Value = '報價單'
       **客戶商品單價單頭資料檔
       TEXT TO select_copmb TEXTMERGE NOSHOW PRETEXT 2
            SELECT * 
              FROM COPMB
             WHERE MB001 = '<<customer_id>>'
               AND MB002 = '<<d2>>'
               AND MB004 = '<<currency_id>>'
               AND (MB018 = '' OR MB018 >= '<<data_date>>')
               AND MB017 <= '<<data_date>>'
       ENDTEXT
       =SQLEXEC(id_key,select_copmb,"v_copmb")       
       **取得單價
       SELECT v_copmb
       IF EOF() THEN 
          d0 = d0+"P"
          ng03 = ng03+1
       ELSE 
          d8 = MB008
          chk_key = MB007
          IF chk_key = 'Y' THEN
             TEXT TO select_copmc TEXTMERGE NOSHOW PRETEXT 2
                  SELECT * 
                    FROM COPMC
                   WHERE MC001 = '<<customer_id>>'
                     AND MC002 = '<<d2>>'
                     AND MC004 = '<<currency_id>>'
                     AND MC005 <= '<<d5>>'
                     AND MC009 <= '<<data_date>>'
                   ORDER BY MC005 DESC
             ENDTEXT      
             =SQLEXEC(id_key,select_copmc,"v_copmc")       
             SELECT v_copmc
             IF EOF() THEN 
             ELSE
                d8 = MC006
             ENDIF
          ENDIF   
       ENDIF
       USE IN v_copmb 
    CASE ThisForm.Combo2.Value = '標準價'
       **品號標準價格檔
       TEXT TO select_invxa TEXTMERGE NOSHOW PRETEXT 2
            SELECT * 
              FROM INVXA 
             WHERE XA017 = 'Y'
               AND XA001 = '<<d2>>'       
       ENDTEXT
       =SQLEXEC(id_key,select_invxa,"v_invxa")
       **取得單價
       SELECT v_invxa
       IF EOF() THEN 
          d0 = d0+"P"
          ng03 = ng03+1
       ELSE 
          IF customer_id = '' OR currency_id = '' THEN
             d0 = d0+'P'
             ng03 = ng03+1
          ELSE
             DO CASE
             CASE XA003 = currency_id 
                  d8 = XA004
             CASE XA005 = currency_id
                  d8 = XA006
             CASE XA007 = currency_id
                  d8 = XA008
             CASE XA009 = currency_id
                  d8 = XA010
             CASE XA011 = currency_id
                  d8 = XA012
             CASE XA013 = currency_id
                  d8 = XA014
             ENDCASE
           ENDIF      
       ENDIF
       USE IN v_invxa
    CASE ThisForm.Combo2.Value = '匯入'
       **EXCEL匯入
       IF ISNULL(sh.CELLS(i,6).Value) THEN
          d0 = d0+"P"
          ng03 = ng03+1
       ELSE
          d8 = sh.CELLS(i,6).Value   
       ENDIF
    ENDCASE

    INSERT INTO new_order (dat0,dat1,dat2,dat3,dat4,dat5,dat6,dat7,dat8,dat9);
                   VALUES (d0,d1,d2,d3,d4,d5,d6,d7,d8,d9)
    
    key_no = key_no+1

NEXT i

sh.WORKBOOKS.close 
sh.QUIT                         &&關閉

ThisForm.Text90.Value = tot_no  &&總筆數
ThisForm.Text22.Value = ng01    &&品號異常
ThisForm.Text20.Value = ng02    &&數量異常
ThisForm.Text21.Value = ng03    &&單價異常

ThisForm.Combo1.LostFocus

SELECT new_order
GOTO TOP

ThisForm.Label00.Click 
ThisForm.Grid1.Refresh 

IF ThisForm.Text90.Value>0 AND ThisForm.Text20.Value=0 AND ThisForm.Text21.Value=0 AND ThisForm.Text22.Value=0 THEN
   ThisForm.Label1.Caption   = "==資料載入完成=="
   ThisForm.Label1.ForeColor = RGB(0,0,0)
   ThisForm.Command1.Enabled = .T.
ELSE
   IF ThisForm.Text90.Value = 0 THEN
      ThisForm.Label1.Caption   = "**品號/客戶品號 有誤，請查明後再進行訂單匯入作業**"
      ThisForm.Label1.ForeColor = RGB(255,0,0)
      ThisForm.Command1.Enabled = .F.
      RETURN
   ELSE
      ThisForm.Label1.Caption   = "**品號/交期/單價有異常狀況，請查明後再進行訂單匯入作業**"
      ThisForm.Label1.ForeColor = RGB(255,0,0)
      ThisForm.Command1.Enabled = .F.
      RETURN
   ENDIF
ENDIF

